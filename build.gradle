plugins {
    id 'groovy'
    id 'java'
    id 'org.owasp.dependencycheck' version '5.0.0'
}

group 'com.boclips'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    testContract {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir 'src/test_contract/groovy'
        }
        resources.srcDir file('src/test_contract/resources')
    }
}

dependencies {
    implementation 'com.mashape.unirest:unirest-java:1.4.9'
    implementation 'org.projectlombok:lombok:1.18.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.10'
    implementation 'org.slf4j:slf4j-api:1.7.25'

    testImplementation 'au.com.dius:pact-jvm-provider_2.11:3.5.6'
    testImplementation 'au.com.dius:pact-jvm-consumer-groovy_2.11:3.5.6'
    testImplementation 'org.yaml:snakeyaml:1.23'

    implementation("com.damnhandy:handy-uri-templates:2.1.8")

    testImplementation("com.damnhandy:handy-uri-templates:2.1.8")

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.1-groovy-2.4'
    testImplementation group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.9'
    testImplementation group: 'net.bytebuddy', name: 'byte-buddy', version: '1.8.22'
    testImplementation group: 'org.objenesis', name: 'objenesis', version: '1.2'

    testContractImplementation sourceSets.main.output

    testContractImplementation configurations.implementation
    testContractImplementation configurations.testImplementation
    testContractImplementation group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.25'

    testContractRuntime configurations.runtime
    testContractRuntime configurations.testRuntime
}

task testContract(type: Test) {
    testClassesDirs = sourceSets.testContract.output.classesDirs
    classpath = sourceSets.testContract.runtimeClasspath
}

apply plugin: 'org.owasp.dependencycheck'

dependencyCheck {
    failBuildOnCVSS = 4

    data {
        directory = './dependency-check-cache'
    }

    analyzers {
        assemblyEnabled = false
    }

    suppressionFile = './dependency-check-suppressions.xml'
}
